Alphabet
 А Б В Г Д Е Ё Ж З И Й К Л М Н О Ө П Р С Т У Ү Ф Х Ц Ч Ш Ь Ы Ъ Э Ю Я
 а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х ц ч ш ь ы ъ э ю я
 %0 1 2 3 4 5 6 7 8 9
 %{ң%}:ң
 %{г%}:0 %{й%}:0 %{а%}:0 ! disappearing stuff
 %{А%}:а %{I%}:ы ! Vow
 ° %% %, %. %- %>:0 %    ! punctuation and boundaries
 ;

Sets

Aleph = А Б В Г Д Е Ё Ж З И Й К Л М Н О Ө П Р С Т У Ү Ф Х Ц Ч Ш Ь Ы Ъ Э Ю Я
        а б в г д е ё ж з и й к л м н о ө п р с т у ү ф х ц ч ш ь ы ъ э ю я ; 

Vow =      и ү э ө а о у ы
           И Ү Э Ө А О У Ы ;

FrontVow = ү э ө
           Ү Э Ө ;

NonVow = Б В Г Д Ж З Й К Л М Н П Р С Т Ф Х Ц Ч Ш Ь Ъ 
         б в г д ж з й к л м н п р с т ф х ц ч ш ь ъ ; ! This is for surface forms only, so no archiphonemes

PalCons =  г ж ш ;

Rules 

!!!! G stuff !!!!

"G epenthesis"
%{г%}:г <=> [ [ :Vow :Vow ] | %{ң%}: ]/:0 _ ;        ! дүүгийн, (нохойн), санг
                                    :й/:0 _ %{А%}: ; ! дүүгээс, нохойгоос


!!!! I stuff !!!!

"{I} as и instead of ы"
%{I%}:и <=> :FrontVow [ :NonVow/:0 ]* _ ;
                          :PalCons/:0 _ ;

"{I} deletion after diphthongs, longvowels, and {ң}"
%{I%}:0 <=>   :й/:0 _ ; ! нохойг, далайг
            :Vow/:0 _ ; ! хүүг, дүүг
          %{ң%}:/:0 _ ; ! үзэсгэлэнг, санг

"I forget what this is for"
н:0 <=> :й/:0 _ %{I%}: ;

"{й} should surface after {I} as и"
%{й%}:й <=> %{I%}:и _ ;


!!!! {A} stuff !!!!

"{A} vowel harmony"
%{А%}:Vy <=> :Vx [ :NonVow/:0 ]* _ ;
             where Vx in ( ү э ө о )
                   Vy in ( э э ө о )
             matched ;

"{A} harmony for 2nd element of long vowels"
%{А%}:э <=> .#. [ :NonVow/:0 ]* :и [ :NonVow/:0 ]* _ ;


!!!! {a} stuff !!!!

"{a} harmony"
%{а%}:Vy <=> :Vx [ :NonVow/:0 ]* [ :т | :д | :с ]/:0 _ ;
             where Vx in ( ү э ө а о у )
                   Vy in ( э э ө а о а )
             matched ;

"I forgot what this {a} rule is for"
%{а%}:э <=> .#. [ :NonVow/:0 ]* :и [ :NonVow/:0 ]* [ :т | :д | :с ]/:0 _ ;
